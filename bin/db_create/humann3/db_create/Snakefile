rule humann3_annotate_genes:
    """
    Use search hits from clustered reps & index table to annotate all
    genome-derep genes. The annotation for each cluster rep is propagated
    to each member of the cluster.
    """
    input:
        hits = config['tmp_dir'] + 'humann3_search/hits.txt',
        tsv = config['tmp_dir'] + 'humann3/clusters_membership.tsv.gz',
        fna = config['tmp_dir'] + 'humann3/genome_reps_filtered.fna.gz',
        faa = config['tmp_dir'] + 'humann3/genome_reps_filtered.faa.gz',
        txt = config['tmp_dir'] + 'humann3/genome_reps_filtered.txt.gz'
    output:
        fna = temp(config['tmp_dir'] + config['uniref_name'] + '/genome_reps_filt_annot.fna'),
        faa = temp(config['tmp_dir'] + config['uniref_name'] + '/genome_reps_filt_annot.faa'),
        tsv = temp(config['tmp_dir'] + config['uniref_name'] + '/genome_reps_filt_annot.tsv'),
    params:
        exe = config['pipeline']['script_folder'] + 'propagate_annotations.py',
        params = config['params']['humann3']['propagate_annotations']
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 59,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 3 * 3 + 11,
    log:
        log_dir + 'humann3_annotate_genes/all.log'
    benchmark:
        benchmark_dir + 'humann3_annotate_genes/all.txt'
    shell:
        """
        OUTDIR=`dirname {output.fna}`
        mkdir -p $OUTDIR 2> {log}
        {params.exe} {params.params} \
          --in-nuc {input.fna} \
          --out-nuc {output.fna} \
          --out-prot {output.faa} \
          {input.hits} {input.faa} \
          {input.txt} {input.tsv} \
          > {output.tsv} 2>> {log}
        """

rule humann3_annotate_hits_copy:
    """
    Copying/compressing query hits (diamond or mmseqs) to the final output directory 
    """
    input:
        hits = config['tmp_dir'] + 'humann3_search/hits.txt'
    output:
        hits = humann3_dir + 'annotation_hits.gz'
    params:
        ionice = config['params']['ionice']
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 59
    log:
        log_dir + 'humann3_annotate_hits_copy/all.log'
    shell:
        """
        ionice {params.ionice} gzip -c {input.hits} > {output.hits} 2> {log}
        """
        
rule humann3_alt_annotate:
    """
    Re-annotating with different UniRef cluster resolution
    """
    input:
        fna = config['tmp_dir'] + config['uniref_name'] + '/genome_reps_filt_annot.fna',
        faa = config['tmp_dir'] + config['uniref_name'] + '/genome_reps_filt_annot.faa',
        tsv = config['tmp_dir'] + config['uniref_name'] + '/genome_reps_filt_annot.tsv',
        idx = ancient(config['cluster_idx'])
    output:
        fna = temp(config['tmp_dir'] + config['uniref_other_name'] + \
                   '/genome_reps_filt_annot.fna'),
        faa = temp(config['tmp_dir'] + config['uniref_other_name'] + \
                   '/genome_reps_filt_annot.faa'),
        tsv = temp(config['tmp_dir'] + config['uniref_other_name'] + \
                   '/genome_reps_filt_annot.tsv')
    params:
        exe = config['pipeline']['script_folder'] + 'uniref_clst_trans.py'
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 59,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 3 * 3 + 9
    log:
        log_dir + 'humann3_annotate_genes/all.log'
    benchmark:
        benchmark_dir + 'humann3_annotate_genes/all.txt'
    shell:
        """
        OUTDIR=`dirname {output.fna}`
        mkdir -p $OUTDIR 2> {log}
        {params.exe} {input.idx} \
          --in-nuc {input.fna} \
          --in-prot {input.faa} \
          --in-tsv {input.tsv} \
          --out-nuc {output.fna} \
          --out-prot {output.faa} \
          --out-tsv {output.tsv} \
          2>> {log} 1>&2
        """    
                
rule humann3_annotate_genes_copy:
    """
    Copying/compressing annotated gene files to the final directory
    """
    input:
        fna = config['tmp_dir'] + '{uniref}/genome_reps_filt_annot.fna',
        faa = config['tmp_dir'] + '{uniref}/genome_reps_filt_annot.faa',
        tsv = config['tmp_dir'] + '{uniref}/genome_reps_filt_annot.tsv'
    output:
        fna = humann3_dir + '{uniref}/genome_reps_filt_annot.fna.gz',
        faa = humann3_dir + '{uniref}/genome_reps_filt_annot.faa.gz',
        tsv = humann3_dir + '{uniref}/genome_reps_filt_annot.tsv.gz'            
    params:
        ionice = config['params']['ionice']
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 59
    log:
        log_dir + 'humann3_annotate_genes_copy/{uniref}.log'
    benchmark:
        benchmark_dir + 'humann3_annotate_genes_copy/{uniref}.txt'
    shell:
        """
        ionice {params.ionice} gzip -c {input.fna} > {output.fna} 2> {log}
        ionice {params.ionice} gzip -c {input.faa} > {output.faa} 2>> {log}
        ionice {params.ionice} gzip -c {input.tsv} > {output.tsv} 2>> {log}
        """
    
rule humann3_bowtie2_build:
    """
    Running bowtie2 build on combined, annotated genes (nucleotide)
    """
    input:
        fna = config['tmp_dir'] + '{uniref}/genome_reps_filt_annot.fna'
    output:
        touch(os.path.join(humann3_dir, '{uniref}/bowtie2_build.done'))
    conda:
        '../../../envs/humann3.yaml'
    threads:
        12
    resources:
        time = lambda wildcards, attempt: attempt * 2 * 60 * 24,
        n = lambda wildcards, attempt, threads: threads,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 2 * 2 + 8,
        lg_idx = lambda wildcards, attempt: '--large-index' if attempt > 1 else ''
    log:
        log_dir + 'humann3_bowtie2_build/{uniref}.log'
    benchmark:
        benchmark_dir + 'humann3_bowtie2_build/{uniref}.txt'
    shell:
        """
        OUTDIR=`dirname {output}`
        PREFIX="$OUTDIR/all_genes_annot"
        bowtie2-build --threads {threads} {resources.lg_idx} \
          {input.fna} $PREFIX 2> {log} 1>&2

        # check that output exists
        IDX_FILES=`find $OUTDIR -maxdepth 1 -name "*.bt2*"`
        IDX_FILES=`echo $IDX_FILES | perl -pe 's/ +/\n/g' | wc -l`
        if [ $IDX_FILES -lt 1 ]; then
          echo "ERROR: no bowtie2 index files found!" >> {log}
          exit 1
        fi
        touch {output} 2>> {log}
        """    

rule humann3_diamond_makedb:
    """
    Running diamond makedb on combined, annotated genes (amino acid)
    """
    input:
        faa = config['tmp_dir'] + '{uniref}/genome_reps_filt_annot.faa'
    output:
        humann3_dir + '{uniref}/protein_database/{dmnd}'
    params:
        tmp_dir = config['tmp_dir']
    conda:
        '../../../envs/humann3.yaml'
    resources:
        time = lambda wildcards, attempt: attempt * 2 * 60 * 24,
        mem_gb_pt = lambda wildcards, attempt: (attempt ** 3 + 2) * 12
    log:
        log_dir + 'humann3_diamond_makedb/{uniref}/{dmnd}.log'
    benchmark:
        benchmark_dir + 'humann3_diamond_makedb/{uniref}/{dmnd}.txt'
    shell:
        """
        PREF=`echo {output} | perl -pe 's/\.[^.]+$//'`
        diamond makedb --in {input.faa} -d $PREF 2> {log} 1>&2
        """    


