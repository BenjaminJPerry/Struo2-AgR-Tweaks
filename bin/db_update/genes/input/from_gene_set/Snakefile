#-- updating gene database with a user-provided set of genes (fasta & metadata) --#

if skipped(config['new_genes']['amino_acid']):
    rule genes_from_user_translate:
        """
        If only nucleotide sequences provided, trans to AA seqs
        """
        input:
            fna = config['new_genes']['nucleotide']        
        output:
            db = temp(config['tmp_dir'] + 'db_update/user-genes-nuc_db'),
            dbt = temp(config['tmp_dir'] + 'db_update/user-genes-nuc_db.dbtype'),
            dbi = temp(config['tmp_dir'] + 'db_update/user-genes-nuc_db.index'),
            dbl = temp(config['tmp_dir'] + 'db_update/user-genes-nuc_db.lookup'),
            dbh = temp(config['tmp_dir'] + 'db_update/user-genes-nuc_db_h'),
            dbht = temp(config['tmp_dir'] + 'db_update/user-genes-nuc_db_h.dbtype'),
            dbhi = temp(config['tmp_dir'] + 'db_update/user-genes-nuc_db_h.index'),
            faa = temp(config['tmp_dir'] + 'db_update/user-genes.faa')
        resources:
            time = lambda wildcards, attempt: attempt ** 2 * 59,
            mem_gb_pt = lambda wildcards, attempt: int(round(attempt ** 2.5 * 8,0))
        conda:
            '../../../../envs/genes.yaml'
        log:
            log_dir + 'genes_from_user_translate/all.log'
        shell:
            """
            mmseqs translatenucs {input.fna} {output.db} 2> {log} 1>&2
            mmseqs convert2fasta {output.db} {output.faa} 2>> {log} 1>&2
            """

if skipped(config['new_genes']['nucleotide']):
    rule genes_from_user_revtranslate:
        """
        If only amino acid sequences provided, rev-trans to get nucleotide
        """
        input:
            faa = config['new_genes']['amino_acid'] 
        output:
            db = temp(config['tmp_dir'] + 'db_update/user-genes-aa_db'),
            dbt = temp(config['tmp_dir'] + 'db_update/user-genes-aa_db.dbtype'),
            dbi = temp(config['tmp_dir'] + 'db_update/user-genes-aa_db.index'),
            dbl = temp(config['tmp_dir'] + 'db_update/user-genes-aa_db.lookup'),
            dbh = temp(config['tmp_dir'] + 'db_update/user-genes-aa_db_h'),
            dbht = temp(config['tmp_dir'] + 'db_update/user-genes-aa_db_h.dbtype'),
            dbhi = temp(config['tmp_dir'] + 'db_update/user-genes-aa_db_h.index'),
            fna = temp(config['tmp_dir'] + 'db_update/user-genes.fna')
        resources:
            time = lambda wildcards, attempt: attempt ** 2 * 59,
            mem_gb_pt = lambda wildcards, attempt: int(round(attempt ** 2.5 * 8,0))
        conda:
            '../../../../envs/genes.yaml'
        log:
            log_dir + 'genes_translate/all.log'
        shell:
            """
            mmseqs translateaa {input.faa} {output.db} 2> {log} 1>&2
            mmseqs convert2fasta {output.db} {output.fna} 2>> {log} 1>&2
            """

def which_genes_combine_fna(wildcards):
    """
    Either user-provided nuc seqs or rev-translated from user-provided AA seqs
    """
    # original seqs
    F = [config['tmp_dir'] + 'db_update/orig.fna']
    # user-provide seqs
    if skipped(config['new_genes']['nucleotide']):
        # must rev-translate or just skip
        if str(config['new_genes']['translate']) == 'True':            
            F.append(config['tmp_dir'] + 'db_update/user-genes.fna')
    else:
        # using user-provide nuc seqs
        F.append(config['new_genes']['nucleotide'])
    return F
            
rule genes_combine_fna:
    """
    Combining new gene set and original gene set.
    """
    input:
        which_genes_combine_fna
    output:
        fna = temp(config['tmp_dir'] + 'db_update/filtered_reps.fna')
    params:
        exe = config['pipeline']['script_folder'] + 'cat_files.py'
    resources:
        time = lambda wildcards, attempt: attempt ** 2 * 59
    log:
        log_dir + 'genes_combine_fna/all.log'
    shell:
        """
        {params.exe} {input} > {output.fna} 2> {log}
        """

def which_genes_combine_faa(wildcards):
    """
    Either user-provided AA seqs or translated from user-provided nuc seqs
    """
    # original seqs
    F = [config['tmp_dir'] + 'db_update/orig.faa']
    ## user-provided seqs
    if skipped(config['new_genes']['amino_acid']):
        # must translate from nuc seqs or just skip
        if str(config['new_genes']['translate']) == 'True':            
            F.append(config['tmp_dir'] + 'db_update/user-genes.faa')
    else:
        # using user-provided seqs
        F.append(config['new_genes']['amino_acid'])
    return F
        
rule genes_combine_faa:
    """
    Combining new gene set and original gene set.
    """
    input:
        which_genes_combine_faa
    output:
        faa = temp(config['tmp_dir'] + 'db_update/filtered_reps.faa')
    params:
        exe = config['pipeline']['script_folder'] + 'cat_files.py'
    resources:
        time = lambda wildcards, attempt: attempt ** 2 * 59
    log:
        log_dir + 'genes_combine_faa/all.log'
    shell:
        """
        {params.exe} {input} > {output.faa} 2> {log}
        """

rule genes_combine_txt:
    """
    Combining new gene set and original gene set.
    """
    input:
        txt1 = config['tmp_dir'] + 'db_update/orig.txt',  # original
        txt2 = config['new_genes']['metadata']            # new 
    output:
        txt = temp(config['tmp_dir'] + 'db_update/filtered_reps.txt')
    params:
        exe = config['pipeline']['script_folder'] + 'cat_files.py'
    resources:
        time = lambda wildcards, attempt: attempt ** 2 * 59
    log:
        log_dir + 'genes_combine_txt/all.log'
    shell:
        """
        {params.exe} --header {input.txt1} {input.txt2} > {output.txt} 2> {log}
        """

rule check_gene_info:
    """
    Copying gene info to the output directory.
    """
    input:
        faa = config['tmp_dir'] + 'db_update/filtered_reps.faa',
        txt = config['tmp_dir'] + 'db_update/filtered_reps.txt'
    output:
        done = genes_dir + 'genome_reps_filtered.check.done'
    params:
        exe = config['pipeline']['script_folder'] + 'check_gene_info.py'
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 59,
        mem_gb_pt = lambda wildcards, attempt: attempt * 8
    log:
        log_dir + 'check_gene_info/all.log'
    benchmark:
        benchmark_dir + 'check_gene_info/all.txt'
    shell:
        """
        {params.exe} {input.faa} {input.txt} 2> {log} 1>&2
        touch {output.done} 2>> {log}
        """    
        
rule copy_gene_info:
    """
    Copying gene info to the output directory.
    """
    input:
        fna = config['tmp_dir'] + 'db_update/filtered_reps.fna',
        faa = config['tmp_dir'] + 'db_update/filtered_reps.faa',
        txt = config['tmp_dir'] + 'db_update/filtered_reps.txt',
        done = genes_dir + 'genome_reps_filtered.check.done'
    output:
        fna = genes_dir + 'genome_reps_filtered.fna.gz',
        faa = genes_dir + 'genome_reps_filtered.faa.gz',
        txt = genes_dir + 'genome_reps_filtered.txt.gz'
    params:
        ionice = config['params']['ionice']
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 59,
        mem_gb_pt = lambda wildcards, attempt: attempt * 8
    log:
        log_dir + 'copy_gene_info/all.log'
    benchmark:
        benchmark_dir + 'copy_gene_info/all.txt'
    shell:
        """
        ionice {params.ionice} gzip -c {input.fna} > {output.fna} 2> {log}
        ionice {params.ionice} gzip -c {input.faa} > {output.faa} 2>> {log}
        ionice {params.ionice} gzip -c {input.txt} > {output.txt} 2>> {log}
        """

rule mmseqs_db_create:
    """ 
    Creating mmseqs2 database that will be used for updating the existing cluster database.
    Using all genes (original and new).
    """
    input:
        faa = config['tmp_dir'] + 'db_update/filtered_reps.faa'
    output:
        db = temp(config['tmp_dir'] + 'db_update/all_genes/genes_db'),
        db_t = temp(config['tmp_dir'] + 'db_update/all_genes/genes_db.dbtype'),
        db_i = temp(config['tmp_dir'] + 'db_update/all_genes/genes_db.index'),
        db_l = temp(config['tmp_dir'] + 'db_update/all_genes/genes_db.lookup'),
        db_s = temp(config['tmp_dir'] + 'db_update/all_genes/genes_db.source'),
        db_h = temp(config['tmp_dir'] + 'db_update/all_genes/genes_db_h'),
        db_ht = temp(config['tmp_dir'] + 'db_update/all_genes/genes_db_h.dbtype'),
        db_hi = temp(config['tmp_dir'] + 'db_update/all_genes/genes_db_h.index')
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 59,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 3 * 20 + 20
    conda:
        '../../../../envs/genes.yaml'
    log:
        log_dir + 'mmseqs_db_create/all.log'
    benchmark:
        benchmark_dir + 'mmseqs_db_create/all.txt'
    shell:
        """
        mmseqs createdb {input.faa} {output.db} 2> {log} 1>&2
        """
